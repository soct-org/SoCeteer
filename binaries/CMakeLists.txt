cmake_minimum_required(VERSION 3.20)

set(CMAKE_TRY_COMPILE_TARGET_TYPE STATIC_LIBRARY)

# The SOCT System provides many variables used throughout the build process.
if (DEFINED SOCT_SYSTEM AND EXISTS "${SOCT_SYSTEM}")
    message(STATUS "Using SOCT system file from SOCT_SYSTEM variable: ${SOCT_SYSTEM}")
else ()
    if (NOT DEFINED _soceteer_root)
        cmake_path(GET CMAKE_CURRENT_LIST_DIR PARENT_PATH _soceteer_root)
    endif ()

    message(WARNING
            "SOCT_SYSTEM variable not defined or file not found.
     This variable should point to a valid SOCTSystem.cmake file generated by SoCeteer.
     The SOCTSystem.cmake file contains important information about the SoC configuration which will be used during the build process and to build the target binaries.
     (Fallback) Locating the latest emitted SOCT system file from ${_soceteer_root}/workspace.")
    file(GLOB_RECURSE SOCT_SYSTEM_FILES CONFIGURE_DEPENDS "${_soceteer_root}/workspace/**/SOCTSystem.cmake")

    if (SOCT_SYSTEM_FILES)
        set(_latest_soct "")
        set(_latest_ts 0)
        foreach (_file IN LISTS SOCT_SYSTEM_FILES)
            # Get last modification timestamp (UTC, seconds since epoch)
            file(TIMESTAMP "${_file}" _ts "%s" UTC)
            if (_ts GREATER _latest_ts)
                set(_latest_ts "${_ts}")
                set(_latest_soct "${_file}")
            endif ()
        endforeach ()
        message(STATUS "Using latest SOCT system file: ${_latest_soct}")
        set(SOCT_SYSTEM "${_latest_soct}")
    else ()
        message(FATAL_ERROR "No SOCTSystem.cmake file found in ${_soceteer_root}/workspace. Please ensure that a SoCeteer project has been built to generate the SOCT system file or set the SOCT_SYSTEM variable to point to a valid SOCTSystem.cmake file.")
    endif ()
endif ()

# Include the SOCT system file to load important variables before project call
include(${SOCT_SYSTEM})

# Print some important variables from the SOCT system
message(STATUS "SOCETEER_ROOT: ${SOCETEER_ROOT}")

set(SOCT_TOOLCHAIN_FILE "${SOCETEER_ROOT}/shared/cmake/toolchain-riscv.cmake")
if (NOT DEFINED CMAKE_TOOLCHAIN_FILE)
    message(STATUS "Using default toolchain file: ${SOCT_TOOLCHAIN_FILE}. To override, set CMAKE_TOOLCHAIN_FILE.")
    include(${SOCT_TOOLCHAIN_FILE})
endif ()

project(RV_Binaries C CXX ASM)

set(ELF_DIR "${SOCETEER_ROOT}/binaries/elfs" CACHE STRING "Directory where to place the compiled programs/bootroms.")

###########################################################################################
# This Cmake can build both programs and bootroms.
# Set BOOTROM_MODE=ON to build bootroms, otherwise programs to execute on the board/simulator.
###########################################################################################
if (DEFINED BOOTROM_MODE)
    message(NOTICE "Configured to build RISC-V bootroms.")
    # Required variables for bootroms:
    set(DTC "dtc" CACHE STRING "Path to the device tree compiler binary (dtc). If not set, looks for dtc in PATH.")

    # Device tree building:
    add_custom_command(
            OUTPUT "${SOCT_DTB}"
            COMMAND ${DTC} -I dts -O dtb -@ -A -o "${SOCT_DTB}" "${SOCT_DTS}"
            DEPENDS "${SOCT_DTS}"
            COMMENT "Building DTB from ${SOCT_DTS}"
    )
    add_custom_target(device_tree ALL DEPENDS "${SOCT_DTB}")

    file(GLOB ALL_ENTRIES RELATIVE "${CMAKE_CURRENT_LIST_DIR}/bootroms" "${CMAKE_CURRENT_LIST_DIR}/bootroms/*/")
else () # PROGRAM MODE
    message(NOTICE "Configured to build RISC-V programs.")

    # Add libgloss
    set(LIBGLOSS_DIR ${SOCETEER_ROOT}/shared/libgloss)
    add_subdirectory(${LIBGLOSS_DIR} ${CMAKE_BINARY_DIR}/libgloss)
    include(${LIBGLOSS_DIR}/cmake/libgloss.cmake) # Include libgloss CMake variables

    file(GLOB ALL_ENTRIES RELATIVE "${CMAKE_CURRENT_LIST_DIR}/programs" "${CMAKE_CURRENT_LIST_DIR}/programs/*/")
endif ()

# Add each program and bootrom as a subdirectory
foreach (ENTRY ${ALL_ENTRIES})
    if (DEFINED BOOTROM_MODE)
        set(BOOTROM ${ENTRY})
        set(_path ${CMAKE_CURRENT_LIST_DIR}/bootroms/${BOOTROM})
        if (EXISTS "${_path}/CMakeLists.txt")
            add_subdirectory(${_path} EXCLUDE_FROM_ALL)
        endif ()
    else ()
        set(PROGRAM ${ENTRY})
        set(_path ${CMAKE_CURRENT_LIST_DIR}/programs/${PROGRAM})
        if (EXISTS ${_path}/CMakeLists.txt)
            add_subdirectory(${_path} EXCLUDE_FROM_ALL)
        endif ()
    endif ()
endforeach ()